plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '9.2.2' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = project.mod_group
    version = project.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'

    base {
        archivesName = "${project.mod_id}-${project.name}"
    }

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        maven { url = 'https://maven.parchmentmc.org/' }
        maven { url = 'https://maven.twelveiterations.com/repository/maven-public/' }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment "org.parchmentmc.data:parchment-${project.parchment_minecraft_version}:${project.parchment_mappings_version}@zip"
        }
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = 'UTF-8'
            options.release = 21
        }
    }

    idea {
        module {
            downloadSources = true
            downloadJavadoc = true
        }
    }
}
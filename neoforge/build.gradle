plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    runs {
        dataClient {
            clientData()
            programArgs '--all', '--mod', project.mod_id
            programArgs '--output', project(':common').file('src/main/generated').absolutePath
            programArgs '--existing', project(':common').file('src/main/resources').absolutePath
        }
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }

    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven { url = 'https://maven.neoforged.net/releases/' }
}

dependencies {
    neoForge "net.neoforged:neoforge:${project.neoforge_version}"

    modApi "net.blay09.mods:balm-neoforge:${project.balm_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    var replaceProperties = [
            mod_id           : mod_id,
            mod_version      : mod_version,
            mod_name         : mod_name,
            mod_description  : mod_description,
            mod_author       : mod_author,
            mod_license      : mod_license,
            mod_icon         : mod_icon,
            minecraft_version: minecraft_version,
            neoforge_version : neoforge_version,
            balm_version     : balm_version
    ]
    inputs.properties replaceProperties

    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }
}

shadowJar {
    exclude '**/more_food/datagen/**'
    exclude '.cache/**'
    from project(':').file('LICENSE.md')

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}